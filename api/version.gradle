buildscript {
    def mcVersionStr = project.name.tokenize("-")[0]
    def (major, minor, patch) = mcVersionStr.tokenize(".")
    def base = major == null || minor == null
    def mcVersion = base ? 11800 : "${major}${minor.padLeft(2, "0")}${(patch ?: "").padLeft(2, "0")}" as int
    def fabric = mcVersion >= 11400
    project.ext.mcVersion = mcVersion
    project.ext.mcVersionStr = mcVersionStr
    project.ext.mcPlatform = fabric ? "fabric" : "forge"
    project.ext.fabric = fabric
    project.ext.forge = !fabric

    repositories {
        gradlePluginPortal()
        maven { url("https://jitpack.io/") }
        maven { url("https://maven.architectury.dev/") }
        maven { url("https://maven.fabricmc.net") }
        maven { url("https://maven.minecraftforge.net") }
        maven { url("https://repo.sk1er.club/repository/maven-public/") }
    }

    dependencies {
        classpath("gg.essential:architectury-loom:0.10.0.2")
        if (!fabric) classpath("dev.architectury.architectury-pack200:dev.architectury.architectury-pack200.gradle.plugin:0.1.3")
        classpath("com.github.replaymod:preprocessor:7746c47")
        classpath("xyz.unifycraft.gradle.unisigning:unisigning:1.0.0")
        classpath("xyz.unifycraft.gradle.unishadow:unishadow:1.0.0")
        classpath("xyz.unifycraft.gradle.unibuild:unibuild:1.0.0")
    }
}

def FABRIC = fabric
def FORGE = !FABRIC

if (FORGE) {
    apply plugin: "dev.architectury.architectury-pack200"
    ext.set("loom.platform", "forge")
}
apply plugin: "gg.essential.loom"

apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "com.replaymod.preprocess"
apply plugin: "xyz.unifycraft.unishadow"
apply plugin: "net.kyori.blossom"
apply plugin: "maven-publish"
apply plugin: "java"
apply plugin: "idea"

archivesBaseName = projectName
version = projectVersion
apply plugin: "xyz.unifycraft.unibuild"

apply plugin: "xyz.unifycraft.unisigning"

compileKotlin.kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"

loom {
    if (FORGE) {
        forge {
            pack200Provider = new dev.architectury.pack200.java.Pack200Adapter()
        }
    }
}

unibuild {
    mcVersion = project.ext.mcVersionStr
    mcPlatform = project.ext.mcPlatform
}

blossom {
    replaceToken("__VERSION__", project.version)
}

configurations {
    modShade
    modImplementation.extendsFrom(modShade)
}

dependencies {
    minecraft("com.mojang:minecraft:" + [
            11802: "1.18.2",
            10809: "1.8.9"
    ][mcVersion])
    mappings([
            11802: "1.18.2+build.1",
            10809: "de.oceanlabs.mcp:mcp_stable:22-1.8.9"
    ][mcVersion])
    if (FORGE) forge("net.minecraftforge:forge:1.8.9-11.15.1.2318-1.8.9")

    shade("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0-native-mt")
    shade("org.jetbrains.kotlin:kotlin-stdlib-jdk8")

    shade("org.kodein.di:kodein-di:7.10.0")

    shade(api("com.github.KevinPriv:keventbus:master-SNAPSHOT"))
    shade("xyz.deftu.deftils:Deftils:1.2.2")
    shade("com.github.ben-manes.caffeine:caffeine:2.9.3")
    shade(api("org.java-websocket:Java-WebSocket:1.5.2"))
    shade(api("xyz.deftu.quicksocket:QuickSocket:1.2.2"))
    shade(api("com.squareup.okhttp3:okhttp:4.9.3"))
    
    compileOnly("org.spongepowered:mixin:0.7.11-SNAPSHOT")

    shade(api("xyz.unifycraft.confide:Confide-${mcVersionStr}-${mcPlatform}:1.0.0")) {
        exclude module: "kotlin-stdlib-jdk8"
        exclude module: "kotlin-reflect"
        exclude module: "elementa-$mcVersionStr-$mcPlatform"
    }
    shade(api("gg.essential:universalcraft-$mcVersionStr-$mcPlatform:181")) {
        exclude module: "kotlin-stdlib-jdk8"
    }
    shade(api("gg.essential:elementa-$mcVersionStr-$mcPlatform:425")) {
        exclude module: "kotlin-stdlib-jdk8"
        exclude module: "kotlin-reflect"
        exclude module: "universalcraft-$mcVersionStr-$mcPlatform"
    }

    compileOnly("org.apache.logging.log4j:log4j-api:2.17.0")
    compileOnly("org.apache.logging.log4j:log4j-core:2.17.0")
}

processResources {
    inputs.property("name", projectName)
    inputs.property("id", projectId)
    inputs.property("version", project.version)

    filesMatching("fabric.mod.json") {
        expand(
                "name": projectName,
                "id": projectId,
                "version": project.version
        )
    }

    filesMatching("mcmod.info") {
        expand(
                "id": projectId,
                "name": projectName,
                "version": project.version,
                "mcversion": minecraft.version
        )
    }

    rename("(.+_at.cfg)", "META-INF/\$1")
}

java {
    withJavadocJar()
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = archivesBaseName
                group = projectGroup
                version = project.version

                artifact(unishadowJar) {
                    builtBy(unishadowJar)
                }
                artifact(javadocJar) {
                    builtBy(javadocJar)
                }
            }
        }
    }
}

classes.dependsOn(processResources)
unishadowJar.dependsOn(applyBuildInfo)